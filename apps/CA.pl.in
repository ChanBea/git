#!{- $config{perl} -}
#
# Wrapper around the ca to make it easier to use
#
# {- join("\n# ", @autowarntext) -}

use strict;
use warnings;
use File::Copy;
use File::Basename qw/basename dirname/;

my $ME = basename($0);
my $openssl = "openssl";
if(defined $ENV{'OPENSSL'}) {
    $openssl = $ENV{'OPENSSL'};
} else {
    $ENV{'OPENSSL'} = $openssl;
}

my $verbose = 1;

my $OPENSSL_CONFIG = $ENV{'OPENSSL_CONFIG'} // "";
my $OPENSSL_CONFIG_P12MNG = "-config /etc/pki/tls/openssl-p12mng.cnf";
my $CERTTOP = $ENV{'CERTTOP'} // "/etc/pki/p12mng";
my $DAYS = "-days 365";
my $CADAYS = "-days 1095";	# 3 years
my $REQ = "$openssl req";
my $CA = "$openssl ca";
my $VERIFY = "$openssl verify";
my $X509 = "$openssl x509";
my $PKCS12 = "$openssl pkcs12";
my $CNAMETXT = "My Certificate";
my $EXPORTPASS_FILE = "exportpw";

# default openssl.cnf file has setup as per the following
my $CATOP = $ENV{'CATOP'} // "/etc/pki/CA";
my $CAKEY = "cakey.pem";
my $CAREQ = "careq.pem";
my $CACERT = "cacert.pem";
my $CACERTDER = "cacert.crt";
my $CACRL = "crl.pem";
my $DIRMODE = 0755;

my $NEWKEY = "newkey.pem";
my $NEWREQ = "newreq.pem";
my $NEWCERT = "newcert.pem";
my $NEWP12 = "newcert.p12";
my $RET = 0;
my $WHAT = shift @ARGV;
my $FILE;
my $PASSOPT = "";

sub help
{
    print STDERR "usage: $ME -newcert|-newreq|-newreq-nodes|-newca|-verify\n";
    print STDERR "       $ME -sign|-xsign|-signcert|-selfsign [-days days]\n";
    print STDERR "       $ME -pkcs12 [certname]\n";
    print STDERR "       $ME -crl|-revoke cert-filename [reason]\n";
    print STDERR "       $ME -p12mng-new cname [certname]\n";
    print STDERR "       $ME -p12mng-revoke cname [reason]\n";
}

# See if reason for a CRL entry is valid; exit if not.
sub crl_reason_ok
{
    my $r = shift;

    if ($r eq 'unspecified' || $r eq 'keyCompromise'
        || $r eq 'CACompromise' || $r eq 'affiliationChanged'
        || $r eq 'superseded' || $r eq 'cessationOfOperation'
        || $r eq 'certificateHold' || $r eq 'removeFromCRL') {
        return 1;
    }
    print STDERR "Invalid CRL reason; must be one of:\n";
    print STDERR "    unspecified, keyCompromise, CACompromise,\n";
    print STDERR "    affiliationChanged, superseded, cessationOfOperation\n";
    print STDERR "    certificateHold, removeFromCRL";
    exit 1;
}

# PEM-format file manipulation - copy, concatenate, remove comments from;
# return like exit status (zero means ok)
sub copy_pemfile
{
    my ($infile, $outfile, $bound, $outmode) = @_;
    my $onthefly = 0;
    my $found = 0;

    $outmode = ">" unless $outmode;
    if ($infile eq $outfile) {
        $onthefly = 1;
        copy($infile,$infile."_tmp") || die "Cannot copy $infile, $!";
        $infile = $infile. "_tmp";
    }
    open IN, $infile || die "Cannot open $infile, $!";
    open OUT, "$outmode$outfile" || die "Cannot write to $outfile, $!";
    while (<IN>) {
        $found = 1 if /^-----BEGIN.*$bound/;
        print OUT $_ if $found;
        $found = 2, last if /^-----END.*$bound/;
    }
    close IN;
    close OUT;
    unlink $infile if $onthefly;
    return $found == 2 ? 0 : 1;
}

# Wrapper around system; useful for debugging.  Returns just the exit status
sub run
{
    my $cmd = shift;
    print "====\n$cmd\n" if $verbose;
    my $status = system($cmd);
    print "==> $status\n====\n" if $verbose;
    return $status >> 8;
}

# Create a certificate along with a private key
sub newcert
{
    $RET = run("$REQ $OPENSSL_CONFIG -new -x509"
            . " -keyout $NEWKEY -out $NEWCERT $DAYS");
    print "Cert is in $NEWCERT, private key is in $NEWKEY\n" if $RET == 0;
}

# Create a certificate request along with a private key
sub newreq
{
    $RET = run("$REQ $OPENSSL_CONFIG -new -keyout $NEWKEY -out $NEWREQ $DAYS");
    print "Request is in $NEWREQ, private key is in $NEWKEY\n" if $RET == 0;
}

# Create a certificate request along with an unencrypted key
sub newreq_nodes
{
    $RET = run("$REQ $OPENSSL_CONFIG -new -nodes"
           . " -keyout $NEWKEY -out $NEWREQ $DAYS");
    print "Request is in $NEWREQ, private key is in $NEWKEY\n" if $RET == 0;
}

# Build a certificate authority(CA)
sub newca
{
    # create the directory hierarchy
    unless (-d "$CATOP") {
        mkdir ${CATOP}, $DIRMODE;
        mkdir "${CATOP}/certs", $DIRMODE;
        mkdir "${CATOP}/crl", $DIRMODE ;
        mkdir "${CATOP}/newcerts", $DIRMODE;
        mkdir "${CATOP}/private", $DIRMODE;
    }
    open OUT, ">${CATOP}/index.txt";
    close OUT;
    open OUT, ">${CATOP}/crlnumber";
    print OUT "01\n";
    close OUT;
    # ask user for existing CA certificate
    print "CA certificate filename (or enter to create)\n";
    $FILE = "" unless defined($FILE = <STDIN>);
    $FILE =~ s{\R$}{};
    if ($FILE ne "") {
        copy_pemfile($FILE,"${CATOP}/private/$CAKEY", "PRIVATE");
        copy_pemfile($FILE,"${CATOP}/$CACERT", "CERTIFICATE");
    } else {
        print "Making CA certificate ...\n";
        $RET = run("$REQ $OPENSSL_CONFIG -new -keyout"
                . " ${CATOP}/private/$CAKEY"
                . " -out ${CATOP}/$CAREQ");
        $RET = run("$CA $OPENSSL_CONFIG -create_serial"
                . " -out ${CATOP}/$CACERT $CADAYS -batch"
                . " -keyfile ${CATOP}/private/$CAKEY -selfsign"
                . " -extensions v3_ca"
                . " -infiles ${CATOP}/$CAREQ") if $RET == 0;
        print "CA certificate is in ${CATOP}/$CACERT\n" if $RET == 0;
    }
    $RET = run("$X509 -in ${CATOP}/$CACERT -inform pem"
            . " -out ${CATOP}/$CACERTDER -outform der") if $RET == 0;
    print "DER format version is in ${CATOP}/$CACERTDER\n" if $RET == 0;
}

# Create a PKCS#12 file from the signed cert and key
sub pkcs12
{
    my $cname = shift;
    $cname = "$CNAMETXT" unless $cname;
    $RET = run("$PKCS12 -in $NEWCERT -inkey $NEWKEY"
            . " -certfile ${CATOP}/$CACERT"
            . " -out $NEWP12 $PASSOPT"
            . " -export -name \"$cname\"");
    print "PKCS#12 file is in $NEWP12\n" if $RET == 0;
}

# Sign a certificate based on a certificate request
sub sign
{
    my ($days,$day) = @_;
    $days = "" unless $days;
    if ($days eq '-days' and $day) {
        $days = join(" ", $days, $day);
    } elsif ($days !~ /^-days[[:blank:]]+\w+/) {
        $days = "";
    }
    $RET = run("$CA $OPENSSL_CONFIG -policy policy_anything"
            . " -out $NEWCERT $days -infiles $NEWREQ");
    print "Signed certificate is in $NEWCERT\n" if $RET == 0;
}

# Same as above except the certificate is writen to STDOUT
sub xsign
{
    my ($days,$day) = @_;
    $days = "" unless $days;
    if ($days eq '-days' and $day) {
        $days = join(" ", $days, $day);
    } elsif ($days !~ /^-days[[:blank:]]+\w+/) {
        $days = "";
    }
    $RET = run("$CA $OPENSSL_CONFIG -policy policy_anything"
            . " $days -infiles $NEWREQ");
}

# Same as sign except it uses configuration file section v3_ca
sub signCA
{
    $RET = run("$CA $OPENSSL_CONFIG -policy policy_anything"
            . " -out $NEWCERT -extensions v3_ca -infiles $NEWREQ");
    print "Signed CA certificate is in $NEWCERT\n" if $RET == 0;
}

# Same as sign except it expects a self signed certificate present in newreq.pem
sub signcert
{
    my ($days,$day) = @_;
    $days = "" unless $days;
    if ($days eq '-days' and $day) {
        $days = join(" ", $days, $day);
    } elsif ($days !~ /^-days[[:blank:]]+\w+/) {
        $days = "";
    }
    $RET = run("$X509 -x509toreq -in $NEWREQ -signkey $NEWREQ"
            . " -out tmp.pem");
    $RET = run("$CA $OPENSSL_CONFIG -policy policy_anything"
            . " -out $NEWCERT $days -infiles tmp.pem") if $RET == 0;
    print "Signed certificate is in $NEWCERT\n" if $RET == 0;
}

# Create self-signed certificate from key
sub selfsign
{
    my ($days,$day) = @_;
    $days = "" unless $days;
    if ($days eq '-days' and $day) {
        $days = join(" ", $days, $day);
    } elsif ($days !~ /^-days[[:blank:]]+\w+/) {
        $days = "";
    }
    $RET = run("$REQ $OPENSSL_CONFIG -new -out $NEWCERT $days"
            . " -x509 -key $NEWKEY");
    print "Self-signed certificate is in $NEWCERT\n" if $RET == 0;
}

# Verify certificates against CA certificate for CATOP 
sub verify
{
    my @files = @ARGV ? @ARGV : ( $NEWCERT );
    my $file;
    foreach $file (@files) {
        my $status = run("$VERIFY \"-CAfile\" ${CATOP}/$CACERT $file");
        $RET = $status if $status != 0;
    }
}

# Prepare directory hierarchy for PKCS#12 management
sub p12mng_prep
{
    # create the directory hierarchy
    unless (-d "$CERTTOP") {
        mkdir ${CERTTOP}, $DIRMODE;
        mkdir "${CERTTOP}/certs", $DIRMODE;
        mkdir "${CERTTOP}/newcerts", $DIRMODE;
        mkdir "${CERTTOP}/private", $DIRMODE;
        print "$CERTTOP created.\n";
    }
    my ($config_p12mng) = $OPENSSL_CONFIG ? ($OPENSSL_CONFIG =~ m|-config[[:blank:]]+(.+)$|) : ($OPENSSL_CONFIG_P12MNG =~ m|-config[[:blank:]]+(.+)$|);
    unless (-f $config_p12mng) {
        print "Please make ${config_p12mng} and run $ME again.\n";
        print "Hint: putting PKCS#12 export password file '$EXPORTPASS_FILE' in\n"
            . " ${CERTTOP}/private will ease management.\n";
        exit 1;
    }
}

# Create a cert/key pair and PKCS#12. This is an application example to
# mass-manage certificates for web client public-key authentication.
sub p12mng_new
{
    p12mng_prep();
    $OPENSSL_CONFIG = $OPENSSL_CONFIG_P12MNG unless $OPENSSL_CONFIG;
    my ($cname,$cnametxt) = @_;
    unless ($cname) {
        print STDERR "Certificate name is required.\n";
        exit 1;
    }
    $cnametxt = "$CNAMETXT" unless $cnametxt;

    if (-f "${CERTTOP}/certs/${cname}.pem") {
        print STDERR "Certificate file ${cname}.pem already exists!\n"
           . "Please run \'$ME -p12mng-revoke $cname\' and try again\n";
        exit 1;
    }
    # create certificates along with a private key
    chdir "${CERTTOP}/newcerts";
    newreq_nodes();
    if ($RET != 0) {
        print STDERR "Request (and private key) generation failed\n";
        exit 1;
    }
    sign($DAYS);
    if ($RET != 0 || ! -f $NEWCERT || -z $NEWCERT) {
        print STDERR "Signing of certificate failed\n";
        exit 1;
    }
    # create a PKCS#12 file from the signed cert and key
    if (-f "${CERTTOP}/private/${EXPORTPASS_FILE}") {
        $PASSOPT = "-passout file:${CERTTOP}/private/${EXPORTPASS_FILE}";
    } else {
        print "Hint: putting PKCS#12 export password file '$EXPORTPASS_FILE' in\n"
            . " ${CERTTOP}/private will ease management.\n";
    }
    pkcs12($cnametxt);
    if ($RET != 0) {
        print STDERR "Creation of PKCS#12 failed\n";
        exit 1;
    }
    # tidy up the keys
    move($NEWP12,"${CERTTOP}/private/${cname}.p12");
    move($NEWKEY,"${CERTTOP}/private/${cname}.key");
    move($NEWCERT,"${CERTTOP}/certs/${cname}.pem");
    unlink $NEWREQ;
    my $msg = <<"EOM";
Keys generated successfully:
${CERTTOP}/certs/${cname}.pem     public key(signed)
${CERTTOP}/private/${cname}.key   private key
${CERTTOP}/private/${cname}.p12   PKCS#12(the bag of above two)
EOM
    print $msg. "\n";
    print "CA public keys are: ${CATOP}/${CACERT}(PEM format), ${CACERTDER}(DER)\n";
}

# Update Certificate Revocation List
sub gencrl
{
    $RET = run("$CA $OPENSSL_CONFIG -gencrl -out ${CATOP}/crl/$CACRL");
    if ($RET != 0) {
        print STDERR "Failed to Generated CRL\n";
        exit 1;
    }
    print "Generated CRL is in ${CATOP}/crl/$CACRL\n";
    # Place what to do after CRL update, e.g.
    # $RET = run("cd ${CERTTOP} && make");
}

# Revoke a certificate
sub revoke
{
    # revoke a certificate
    my ($cname,$reason) = @_;
    unless ($cname) {
        print "Certificate filename is required; reason optional.\n";
        exit 1;
    }
    $reason = "" unless defined $reason;
    if (length($reason) gt 0 && crl_reason_ok($reason)) {
        $reason = " -crl_reason $reason"
    }
    unless (-f "$cname") {
        print STDERR "No such file $cname\n";
        exit 1;
    }
    $RET = run("$CA $OPENSSL_CONFIG -revoke \"$cname\"" . $reason);
    if ($RET != 0) {
        print STDERR "Failed to revoke $cname\n";
        exit 1;
    }
}

# Revoke and delete a set of cert, key and PKCS#12 files
sub p12mng_revoke
{
    $OPENSSL_CONFIG = $OPENSSL_CONFIG_P12MNG unless $OPENSSL_CONFIG;
    my ($cname,$reason) = @_;
    unless ($cname) {
        print "Certificate name is required; reason optional.\n";
        exit 1;
    }
    $reason = "" unless defined $reason;
    unless (-f "${CERTTOP}/certs/${cname}.pem") {
        print STDERR "No such file ${cname}.pem\n";
        exit 1;
    }
    revoke("${CERTTOP}/certs/${cname}.pem",$reason);
    # remove the revoked key set
    run("rm -i ${CERTTOP}/certs/${cname}.pem");
    run("rm -i ${CERTTOP}/private/${cname}.key");
    run("rm -i ${CERTTOP}/private/${cname}.p12");
    print "cname $cname successfully revoked.\n";
    gencrl();
}


unless ($WHAT) {
    help();
    exit 0;
}
if ( $WHAT =~ /^(-\?|-h|-help)$/ ) {
    help();
    exit 0;
}
if ($WHAT eq '-newcert' ) {
    newcert();
} elsif ($WHAT eq '-newreq' ) {
    newreq();
} elsif ($WHAT eq '-newreq-nodes' ) {
    newreq_nodes();
} elsif ($WHAT eq '-newca' ) {
    newca();
} elsif ($WHAT eq '-pkcs12' ) {
    pkcs12($ARGV[0]);
} elsif ($WHAT eq '-xsign' ) {
    xsign($ARGV[0],$ARGV[1]);
} elsif ($WHAT eq '-sign' ) {
    sign($ARGV[0],$ARGV[1]);
} elsif ($WHAT eq '-signCA' ) {
    signCA();
} elsif ($WHAT eq '-signcert' ) {
    signcert($ARGV[0],$ARGV[1]);
} elsif ($WHAT eq '-selfsign' ) {
    selfsign($ARGV[0],$ARGV[1]);
} elsif ($WHAT eq '-verify' ) {
    verify();
} elsif ($WHAT eq '-p12mng-new' ) {
    p12mng_new($ARGV[0],$ARGV[1]);
} elsif ($WHAT eq '-crl' ) {
    gencrl();
} elsif ($WHAT eq '-revoke' ) {
    revoke($ARGV[0],$ARGV[1]);
} elsif ($WHAT eq '-p12mng-revoke' ) {
    p12mng_revoke($ARGV[0],$ARGV[1]);
} else {
    print STDERR "Unknown arg \"$WHAT\"\n";
    print STDERR "Use -help for help.\n";
    exit 1;
}

exit $RET;
